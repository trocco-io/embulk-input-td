plugins {
    id "java"
    id "checkstyle"
    id "jacoco"
    id "pmd"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.7.0"
    id "com.palantir.git-version" version "3.1.0"
    id "com.diffplug.spotless" version "5.15.0"
    id "com.github.johnrengelman.shadow" version "6.1.0" apply false
}

repositories {
    mavenCentral()
}

group = "trocco-io"
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^v?[0-9]+\.[0-9]+\.[0-9]+([.-][.a-zA-Z0-9-]+)?/) {
        vd.lastTag.replaceFirst(/^v/, "")
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()
description = "Loads records from Td."

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

dependencies {
    def embulkVersion = "0.10.42"
    compileOnly("org.embulk:embulk-api:${embulkVersion}")
    compileOnly("org.embulk:embulk-spi:${embulkVersion}")
    implementation "org.embulk:embulk-util-config:0.3.4"
    implementation "com.google.guava:guava:33.4.8-jre"
    implementation "com.google.inject:guice:7.0.0"

    implementation project(path: ":shadow-td-client", configuration: "shadow")
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial"
}

embulkPlugin {
    mainClass = "org.embulk.input.td.TdInputPlugin"
    category = "input"
    type = "td"
}

gem {
    authors = ["Muga Nishizawa"]
    email = ["muga.nishizawa@gmail.com"]
    summary = "Td input plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-input-td"
    licenses = ["MIT"]
}

def checkstyleConfigDir = new File(rootDir, "/config/checkstyle")
checkstyle {
    configFile = new File(checkstyleConfigDir, "checkstyle.xml")
    configProperties.checkstyleConfigDir = checkstyleConfigDir
    toolVersion = '7.6.1'
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
check.dependsOn('checkstyle')

// PMD
pmd {
    ignoreFailures = false
}

jacocoTestReport {
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        // googleJavaFormat()
        toggleOffOn()
    }
}

// Workaround for IntelliJ issue
// https://youtrack.jetbrains.com/issue/IDEA-163411
// https://github.com/johnrengelman/shadow/issues/264#issuecomment-814402906
compileJava.dependsOn(tasks.getByPath(":shadow-td-client:publishToMavenLocal"))

